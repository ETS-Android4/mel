// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.KOTLIN_V = '1.3.50'
    ext.KOTLIN_CR_V = '1.3.0-RC'
    // get latest commit
    ext.COMMIT = { ->
        def out = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', 'HEAD'
            standardOutput = out
        }
        out.toString().take(7).trim()
    }.call()
    ext.VERSION = { ->
        File f = new File(projectDir, "build.tmp.version")
        println "trying to read version from ${f.getCanonicalPath()}"
        if (f.exists()) {
            f.readLines().get(0)
        } else
            "version not provided"
    }.call()
    println "commit=${ext.COMMIT} version=\"${ext.VERSION}\""
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://maven.google.com"
        }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_V"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$KOTLIN_V"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    // this builds the jars
    id 'org.springframework.boot' version '2.1.9.RELEASE'
}

project.ext {
    System.out.println("building with Kotlin version: $KOTLIN_V")
    javaFxVersion = "12.0.1"
    javaFxModules = ['javafx.controls', 'javafx.fxml', 'javafx.swing', 'javafx.web']
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


