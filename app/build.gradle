apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
}
buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
    }
}
// define mel specific stuff here
def keyPropFile = rootProject.file("keystore.properties")
ext {
    signed = false
    buildVersion = new Date().getTime().toString()
    buildVariant = "apk"
}
android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "de.mel.mel"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "0.1-beta"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "${applicationId}")
    }
    signingConfigs {

    }
    compileOptions {
        encoding 'UTF-8'
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    buildTypes {
        release {
            manifestPlaceholders = [appName: "Mel", icon: "ic_launcher"]
            buildConfigField("String", "BUILD_VARIANT", "\"apk-release\"")
            buildConfigField("String", "BUILD_VERSION", "\"${buildVersion}\"")
            minifyEnabled false
        }
        debug {
            buildConfigField("String", "BUILD_VARIANT", "\"apk-debug\"")
            buildConfigField("String", "BUILD_VERSION", "\"${buildVersion}\"")
            minifyEnabled false
        }
    }
    // write the additional prop file
    afterEvaluate {
        assembleRelease.finalizedBy(releaseWithProps)
    }
}

// this writes the prop file miniserver needs
task releaseWithProps {
    dependsOn 'assembleRelease'
    doLast {
        println("RELEASE WITH PROPS")
        Properties p = new Properties()
        p['version'] = project.buildVersion
        p['variant'] = 'apk-release'
        File output = new File("$buildDir/outputs/apk/release")
        new File(output, "${archivesBaseName}-release.apk.properties")
                .withWriter { w -> p.store(w, null) }
    }
}

// life is pointless without those assets
task copyToAssets(type: Copy) {
    println "copying assets..."
    def auth = rootProject.file('auth/src/main/resources')
    def filesync = rootProject.file('filesync/src/main/resources')
    def contacts = rootProject.file('contacts/src/main/resources')
    def assets = rootProject.file('app/src/main/assets')
    from(auth)
    from(filesync)
    from(contacts)
    into(assets)
}

preBuild {
    String taskName = gradle.startParameter.taskNames[0]
    taskName = taskName.split(':').last()
    println("prebuild() for task $taskName")
    if (taskName == 'assembleRelease') {
        println("release() -> clean()")
        dependsOn 'clean'
    }
    dependsOn 'copyToAssets'
}

dependencies {
    // mel stuff
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':auth')
    implementation project(':contacts')
    implementation project(':filedump')
    implementation project(':filesync')
    // external stuff
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.core:core:1.2.0-alpha04'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
}



